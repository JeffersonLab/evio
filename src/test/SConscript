import os	

# scons build file for evio
Import('env', 'platform', 'archDir', 'incInstallDir', 'libInstallDir', 'binInstallDir', 'archIncInstallDir', 'execLibs', 'debugSuffix', 'disruptorHome')


# lists of all C++ files
ccfileList = Glob('*.cpp', strings=True)


boostThdLib = 'boost_thread'
if platform == 'Darwin':
    boostThdLib = 'boost_thread-mt'

LIBS    = ['eviocc'+debugSuffix, 'Disruptor','boost_system', boostThdLib,  'boost_chrono', "boost_filesystem",  execLibs]
LIBPATH = ['#src/libsrc++/'+archDir, '/usr/lib64', '/usr/lib', '/usr/local/lib', str(disruptorHome)+'/build/Disruptor']
CPPPATH = ['#src/libsrc++', disruptorHome, '/usr/local/include','/usr/include/boost', '/usr/local/include/boost']

# If env var BOOST_ROOT is defined, allow to override other locations
BOOST_ROOT = os.environ.get('BOOST_ROOT', '')
if BOOST_ROOT != '':
    LIBPATH.insert(0,BOOST_ROOT+"/lib")
    CPPPATH.insert(0,BOOST_ROOT+"/include")

# Compile C++ files and install
for fileName in ccfileList:
    compiledFile = env.Program(source  = fileName,
                            LIBS = LIBS,
                            LIBPATH = LIBPATH,
                            CPPPATH = CPPPATH)

    if 'install' in COMMAND_LINE_TARGETS:
        env.Alias('install', env.Install(target = binInstallDir, source = compiledFile))
