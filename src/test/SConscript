# scons build file for evio
Import('env', 'binInstallDir', 'archDir', 'execLibs', 'debugSuffix')
import re

 # lists of all C and C++ files
#cfileList  = ['evtest.c', 'evtest2.c', 'evtest3.c', 'evtestBuf.c', 'evtestSock.c', 'evtestRead.c']
#ccfileList  = []
cfileList  = Glob('*.c',  strings=True)
ccfileList = Glob('*.cc', strings=True)


# For each C file, create executable of the same name,
# and install that executable if "scons examples" is command
for fileName in cfileList:
    compiledFile = env.Program(source  = fileName,
                               LIBS    = ['evio'+debugSuffix, 'pthread'] + execLibs,
                               LIBPATH = ['#src/libsrc/'+archDir],
                               CPPPATH = ['.', '#src/libsrc'])
    env.Alias('tests', env.Install(target = binInstallDir, source = compiledFile))

# If we store all compiled programs in a list and then
# install the list itself, for some unknown reason the
# whole variant library is copied into the install directory.
# So we do things individual file by individual file.

# compile and install
for fileName in ccfileList:
    compiledFile = env.Program(source  = fileName,
                               LIBS    = ['evio'+debugSuffix,
                                          'evioxx'+debugSuffix] + execLibs,
                               LIBPATH = ['#src/libsrc/'+archDir,
                                          '#src/libsrc++/'+archDir],
                               CPPPATH = ['.', '#src/libsrc', '#src/libsrc++'])
    env.Alias('tests', env.Install(target = binInstallDir, source = compiledFile))
