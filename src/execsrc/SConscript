# scons build file for evio
Import('env', 'libDir', 'binDir', 'archDir', 'execLibs', 'debugSuffix')
import re
import os

# lists of all C and C++ files
cfileList  = Glob('*.c',  strings=True)
ccfileList = Glob('*.cc', strings=True)

# For each C file, create executable of the same name,
# and install that executable if "scons examples" is command
for fileName in cfileList:
    compiledFile = env.Program(source  = fileName,
                               LIBS    = ['evio'+debugSuffix] + execLibs,
                               LIBPATH = ['#src/libsrc/'+archDir, libDir],
                               CPPPATH = ['.', '#src/libsrc'])
    env.Alias('install', env.Install(target = binDir, source = compiledFile))


# If we store all compiled programs in a list and then
# install the list itself, for some unknown reason the
# whole variant library is copied into the install directory.
# So we do things individual file by individual file.

# compile and install
for fileName in ccfileList:
    if(os.getenv('ETROOT')!=None):
        compiledFile = env.Program(source  = fileName,
                                   LIBS    = ['evio'+debugSuffix,'evioxx'+debugSuffix, 'et'+debugSuffix] + execLibs,
                                   LIBPATH = ['#src/libsrc/'+archDir,'#src/libsrc++/'+archDir, libDir],
                                   CPPPATH = ['.', '#src/libsrc', '#src/libsrc++'])
    else:
        compiledFile = env.Program(source  = fileName,
                                   LIBS    = ['evio'+debugSuffix,'evioxx'+debugSuffix] + execLibs,
                                   LIBPATH = ['#src/libsrc/'+archDir,'#src/libsrc++/'+archDir, libDir],
                                   CPPPATH = ['.', '#src/libsrc', '#src/libsrc++'])
    env.Alias('install', env.Install(target = binDir, source = compiledFile))
        
